~ start

**It's a man.**
** He is old, rough and trimmed silver hair, seemingly unbothered by the biting cold.**
**He sits and one by one puts his stiff legs into the car, shutting the door with just enough force.**
Old Man: Deepest thanks, truly... these old knees of mine really started winning the argument halfway up the trail... there really is no point arguing with them, these days.
Old Man: Used to tell 'em 'just one more mile' and they'd listen.
Old Man: Now?... Now they just start screaming back at me until I'm forced to listen to them instead!
**The old man looks cheerful. Despite his complaint, there is a light smile on his face.**
**He looks around attentively at each corner of the car... then, locks his gaze on you.**
Old Man: You've got a heavy grip on that steering wheel, son... and an even heavier look behind those eyes.
Old Man: Whatever you're carrying up this mountain... it looks like it weighs more than this car.

- I guess I've had a lot on my mind lately. It's been a long road.
	do DialogueFlow.choice("positive")
	=> step1_good

- I'm just focused. It has been a long road and I don't want to make any wrong turns.
	do DialogueFlow.choice("negative")
	=> step1_bad

- Don't talk much, it's distracting and i'm trying not to go the wrong way.
	do DialogueFlow.choice("negative")
	=> step1_bad

- Actually, I have changed my mind. I cannot take you with me, please get out.
	Old Man: I... I see...
	**The old man slowly climbs out of the car, left alone on the roadside.**
	=> END

# ===== STEP 1 GOOD BRANCH =====
~ step1_good

**The man now looks through the windshield, into the misty road.**
**He sighs.. his joyful tone is now just a little more wistful.**
Old Man: I suppose it has been.
Old Man: But, you know.. sometimes it's not that the road is long.
Old Man: Maybe it feels long because you're tired of carrying so much luggage with you.. it adds up over time.

- Well, I guess you're right, but how would you know?
	do DialogueFlow.choice("positive")
	=> step2_good

- Whatever man, if you say so.
	do DialogueFlow.choice("negative")
	=> step2_bad

- I didn't pick you up for a therapy session, did I? Get out of my car.
	Old Man: I understand...
	**The old man slowly climbs out of the car, left alone on the roadside.**
	=> END

# ===== STEP 1 BAD BRANCH =====
~ step1_bad

**The man now looks through the windshield, into the misty road.**
**He keeps his joyful tone.**
Old Man: I hear you, son.. sometimes the engine is too loud for talk anyway.
Old Man: ...
Old Man: But I wouldn't worry too much about going the wrong way.. there are always routes you can pick that get you back on track.
Old Man: And luckily.. I happen to know them all!
**He now keeps a proud smile on his face.**
Old Man: Feel free to ask if you need any help.. after all, the best travelers sometimes need someone to hold the map.
Old Man: And I feel.. I feel you might need someone to do that for you, don't you?

- Well, I guess you're right, but how would you know?
	do DialogueFlow.choice("positive")
	=> step2_good

- Whatever man, if you say so.
	do DialogueFlow.choice("negative")
	=> step2_bad

- I didn't pick you up for a therapy session, did I? Get out of my car.
	Old Man: I understand...
	**The old man slowly climbs out of the car, left alone on the roadside.**
	=> END

# ===== STEP 2 GOOD BRANCH =====
~ step2_good

**He lets out a giggle.**
Old Man: Well, let's just say.. I've always been a man who took pride in his own two legs.
Old Man: For as long as I can remember, I've walked these trails back and forth.. never needing a soul to help me along the way.
Old Man: But time..
**His bright voice now becomes more grim.**
Old Man: oh..
Old Man: .. time weighs.
Old Man: And yet.. I stubbornly kept my ways, tackling this climb alone.
Old Man: But then comes the day you can no longer hold that weight.
Old Man: And you.. you look like you can no longer hold it by yourself.

- I'm too close to the summit now. It's a bit late in the climb to start looking for a hand to hold, don't you think?
	do DialogueFlow.choice("positive")
	=> step3_good

- Thank you for sharing that. I'm glad you overcame that stubborness of yours in time.
	do DialogueFlow.choice("positive")
	=> step3_good

- It's too late for me. There's nothing you can do. I'm almost at the peak, anyway.
	do DialogueFlow.choice("negative")
	=> step3_bad

- Stop being so pityful, it's annoying. I'm just gonna leave you here, get out.
	Old Man: I... I see...
	**The old man slowly climbs out of the car, left alone on the roadside.**
	=> END

# ===== STEP 2 BAD BRANCH =====
~ step2_bad

**His bright voice now becomes more grim.**
Old Man: Well, you don't have to be such a tough nut to crack, do you?
Old Man: But who am I to judge.. I used to have that stubborness with me too up until recently. Took me a while.. but I eventually gave in.
Old Man: ...
Old Man: You see.. I've walked these tracks for as long as I can remember.. and I took pride.. so much pride in my ability to climb this whole thing.. and fast!
Old Man: Faster than anyone!
**His bright voice now becomes more grim.**
Old Man: But time.. is weighing too much.
Old Man: And you.. whatever it is that you're carrying with you.. looks just as heavy.

- I'm too close to the summit now. It's a bit late in the climb to start looking for a hand to hold, don't you think?
	do DialogueFlow.choice("positive")
	=> step3_good

- Thank you for sharing that. I'm glad you overcame that stubborness of yours in time.
	do DialogueFlow.choice("positive")
	=> step3_good

- It's too late for me. There's nothing you can do. I'm almost at the peak, anyway.
	do DialogueFlow.choice("negative")
	=> step3_bad

- Stop being so pityful, it's annoying. I'm just gonna leave you here, get out.
	Old Man: I... I see....
	**The old man slowly climbs out of the car, left alone on the roadside.**
	=> END

# ===== STEP 3 GOOD BRANCH (ENDING) =====
~ step3_good

**He calmly laughs.**
Old Man: Ahah.. it's never too late, my friend.
Old Man: You have this stubborn old soul sitting next you.. who finally became a little less stubborn.. and asked for help for the first time...
Old Man: Today!
Old Man: For the first time.. i'm not climbing alone.. and asked for help.
Old Man: And now.. how I wish I had done that sooner..
Old Man: ..
Old Man: Oh!
Old Man: Wait, stop.. we're here! You can drop me off here.
Old Man: Thank you traveler, for your help!
Old Man: Be on your way now.. and don't forget...
Old Man: There's no shame in catching a ride when the road gets long.
**Just as slowly as he got in, the man left the car.**
**This time, he closes it firmly.**

do DialogueFlow.empathy += 20
=> END

# ===== STEP 3 BAD BRANCH (ENDING) =====
~ step3_bad

**His look becomes gloomier.**
**His voice, more somber.**
Old Man: There really is nothing I can do, son.
Old Man: Only you can..
Old Man: But you always can.
Old Man: ..
Old Man: Oh!
Old Man: Wait, stop.. we're here! You can drop me off here.
Old Man: Thank you traveler, for your help!
Old Man: Be on your way now.. and don't forget...
Old Man: There's no shame in catching a ride when the road gets long.
**Just as slowly as he got in, the man left the car.**
**This time, he closes it firmly.**

do DialogueFlow.empathy -= 10
=> END
